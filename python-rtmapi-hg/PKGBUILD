# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=python-rtmapi-hg
_pyname=RtmAPI
pkgver=65
pkgrel=2
pkgdesc='API package for rememberthemilk.com  (hg version)'
arch=('any')
url='http://pypi.python.org/pypi/RtmAPI/'
license=('MIT')
depends=('python' 'python-httplib2')
makedepends=('mercurial')
options=(!emptydirs)
provides=('python-rtmapi')
conflicts=('python-rtmapi')
source=("LICENSE")
md5sums=('7d6fd7c8e022bae9010af272c31eb45f')

_hgroot=https://bitbucket.org/michaelgruenewald/rtmapi
_hgrepo=rtmapi

package() {
  cd "${srcdir}"
  msg "Connecting to Mercurial server...."

  if [[ -d "${_hgrepo}" ]]; then
    cd "${_hgrepo}"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "${_hgroot}" "${_hgrepo}"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_hgrepo}-build"
  cp -r "${srcdir}/${_hgrepo}" "${srcdir}/${_hgrepo}-build"
  cd "${srcdir}/${_hgrepo}-build"

  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
