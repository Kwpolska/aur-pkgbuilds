# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=python2-yapsy-hg
pkgver=245.4bf9fda232f4
pkgrel=1
pkgdesc='Yet another plugin system  (hg version)'
arch=('any')
url='http://yapsy.sourceforge.net/'
license=('BSD')
depends=('python2')
makedepends=('mercurial')
options=(!emptydirs)
provides=('python2-yapsy')
conflicts=('python2-yapsy')
source=("hg+http://hg.code.sf.net/p/yapsy/code")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/code"

  # python2 shebang
  sed -i 's|bin/python$|&2|' `grep -rl bin/python`
}

package() {
  cd "${srcdir}/code/package"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

pkgver() {
  cd "${srcdir}/code"
  hg identify -ni | awk 'BEGIN{OFS=".";} {print $2,$1}'
}

# vim:set ts=2 sw=2 et:
