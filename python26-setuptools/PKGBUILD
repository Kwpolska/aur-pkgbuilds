# $Id: PKGBUILD 212111 2014-05-07 16:46:33Z fyan $
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python26-setuptools
pkgname=python26-setuptools
pkgver=3.6
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
makedepends=('python26')
source=("http://pypi.python.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz")
sha512sums=('8233a25229ebb97a2cd78b884d6ae41b441022a0ff28495832183b90a86abdc3216c9f7f514639c4a9a77848c8f94cfb072d676c2e382496162a52732e7e3f58')

prepare() {
  cd "${srcdir}"/setuptools-${pkgver}
  sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/python2.6|" setuptools/tests/test_resources.py
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2.6|" setuptools/command/easy_install.py
}

build() {
  # Build python 2 module
  cd "${srcdir}"/setuptools-${pkgver}
  python2.6 setup.py build
}

package() {
  depends=('python26')

  cd "${srcdir}/setuptools-${pkgver}"
  python2.6 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  rm "${pkgdir}/usr/bin/easy_install"
}
