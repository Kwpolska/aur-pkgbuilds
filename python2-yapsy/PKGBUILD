# Maintainer: Chris “Kwpolska” Warrick <chris@chriswarrick.com>
pkgname=python2-yapsy
_pyname=Yapsy
pkgver=1.10.423
pkgrel=1
pkgdesc='Yet Another Plugin SYstem'
arch=('any')
url='http://yapsy.sourceforge.net/'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=("http://pypi.python.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
md5sums=('fb0e0afbfc1e6086e7d34e093cf3f294')

prepare() {
  cd "${srcdir}/${_pyname}-${pkgver}"

  # python2 shebang
  sed -i 's|bin/python$|&2|' `grep -rl bin/python`
}

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 src2/package/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
